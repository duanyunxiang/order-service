plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.dyx'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    // 指定Testcontainers版本，用于配置测试用、轻量级的postgreSql容器
    set('testcontainersVersion',"1.17.3")
    set('springCloudVersion', "2021.0.8")
    set('testKeycloakVersion', "3.2.0")
    // OpenTelemetry版本
    set('otelVersion',"1.17.0")
}

dependencies {
    // 支持反应式应用的持久化
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // 构建反应式web应用，Netty作为嵌入式服务器
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    // 引入配置服务
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.retry:spring-retry'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'

    runtimeOnly 'org.postgresql:postgresql'
    //引入flyway，最新版本不支持postgreSql 14.4
    runtimeOnly 'org.flywaydb:flyway-core:9.22.3'
    runtimeOnly 'org.springframework:spring-jdbc'
    // OpenTelemetry代理会通过字节码动态插装Java代码
    runtimeOnly "io.opentelemetry.javaagent:opentelemetry-javaagent:${otelVersion}"

    implementation 'org.springframework.cloud:spring-cloud-stream-binder-rabbit'
    // 引入OAuth2
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    // 使用spring boot actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    // 集成Grafana观测工具prometheus，用于收集和存储应用metrics度量信息
    implementation 'io.micrometer:micrometer-registry-prometheus'

    annotationProcessor 'org.projectlombok:lombok'
    // 自定义配置属性，自动补全和类型检查支持
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'com.squareup.okhttp3:mockwebserver'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation "com.github.dasniko:testcontainers-keycloak:${testKeycloakVersion}"
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:r2dbc'
    testImplementation("org.springframework.cloud:spring-cloud-stream") {
        artifact {
            name = "spring-cloud-stream"
            extension = "jar"
            type ="test-jar"
            classifier = "test-binder"
        }
    }

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootBuildImage {
    builder = "docker.io/paketobuildpacks/builder-jammy-base:0.4.302"
    imageName = "${project.name}"
    environment = ["BP_JVM_VERSION": "17"]
}

tasks.named('test') {
    useJUnitPlatform()
}
